<?xml version="1.0" encoding="utf-8"?>
<mx:Application 
	xmlns:mx="http://www.adobe.com/2006/mxml" 
	layout="absolute"
  width="800"
  height="600"
  backgroundColor="0xbb9999"
  verticalScrollPolicy="off"
  frameRate="30"
  applicationComplete="init();"
  implements="core.IGame">
	
	<mx:Style>
		Alert, Panel, TitleWindow {
			cornerRadius: 0;
			
			backgroundColor: white;
			backgroundAlpha: 0.7;
			
			borderColor: white;
			borderAlpha: 0.7;
			
			color: black;
			
			titleStyleName: "title";
		}
		
		TitleWindow {
	   closeButtonUpSkin:       Embed(source="assets/close_up.swf");
	   closeButtonDisabledSkin: Embed(source="assets/close_up.swf");
	   closeButtonOverSkin:     Embed(source="assets/close_over.swf");
	   closeButtonDownSkin:     Embed(source="assets/close_down.swf");
		}
		
		Button {
			cornerRadius: 0;
			
			themeColor: #8b6e6e;
			
			borderColor: white;
			
			fillColors: #ccaaaa, #ccaaaa;
			fillAlphas: 1.0, 1.0;
			highlightAlphas: 0.0, 0.0;
			focusAlpha: 0.0;
			
			letterSpacing: 1;
			fontSize: 9;
			fontWeight: normal;
			fontAntiAliasType: normal;
			fontSharpness: 400;
		}
		
		ScrollBar {
			highlightAlphas: 0.0, 0.0;
			trackColors: #997777, #997777;
		}
		
		Text, Label, .title {
			fontSize: 9;
			fontWeight: normal;
			fontAntiAliasType: normal;
			fontSharpness: 400;
		}
		
		.title {
			fontSize: 11;
			letterSpacing: 1;
		}
	</mx:Style>
	
	<mx:Script>
		<![CDATA[
			import core.*;
			import units.*;
			import utils.LinkedList.*;
			import utils.Vector2;
			import utils.Constants;
			import utils.FPS;
			import __AS3__.vec.Vector;
			import mx.controls.Image;
			import mx.controls.Text;
			import mx.controls.Alert;
			import mx.controls.ProgressBar;
			import mx.containers.Canvas;
			import mx.modules.ModuleManager;
			import mx.managers.PopUpManager;
			import mx.events.ModuleEvent;
			import mx.events.CloseEvent;
			import mx.modules.IModuleInfo;
			import mx.effects.Fade;
			import mx.effects.easing.Cubic;
			
			/////////////////////////////////////////////////////////////////////////
			// CONSTANTS
			/////////////////////////////////////////////////////////////////////////
			
			public static const MAIN_WIDTH:Number = 400;
			public static const MIN_LEVEL_HEIGHT:Number = 600;
			public static const MAX_LEVEL_HEIGHT:Number = 6000;
			
			public static const TICKS_PER_SECOND:int = 30;
			public static const DT:Number = 1.0 / TICKS_PER_SECOND;
			
			public static const LAUNCH_TIME_MS:Number = 2000.0;
			
			public static const F_DRAG:Number = 0.01;
			public static const MAX_COLLISION_ITERATIONS:int = 2;
			public static const RESTITUTION:Number = 0.3;
			
			public static const VICTORY_DISTANCE:Number = 20.0;
			
			/////////////////////////////////////////////////////////////////////////
			// VARIABLES
			/////////////////////////////////////////////////////////////////////////
			
			/** For loading modules. */
			protected var moduleInfo:IModuleInfo;

			/** For uploading files. */
			protected var file:FileReference;
			
			/** The level editor. */
			protected var levelEditor:LevelEditor;
			
			/** Timer for the game loop. */
			protected var loopTimer:Timer;
			
			/** Timer for virus launch. */
			protected var launchTimer:Timer;
			// The number of 'launchTimer' ticks til 'LAUNCH_TIME_MS'.
			private var countsToLaunch:int;
			
			protected var bodies:LinkedList;
			
			protected var finishes:Vector.<Finish>;
			
			/** The player-controlled virus. */
			protected var virus:Virus;
			
			/** The list node of the current virus-infected Body object, if any. */
			protected var infected:Node;
			
			protected var isUpPressed:Boolean     = false;
			protected var isDownPressed:Boolean   = false;
			protected var isLeftPressed:Boolean   = false;
			protected var isRightPressed:Boolean  = false;
			
			/** Is the space bar currently pressed? */
			protected var isSpacePressed:Boolean  = false;
			/** Was the space bar pressed last frame? */
			protected var wasSpacePressed:Boolean = false;
			
			/** @see core.IGame.getMinLevelHeight(). */
			public function getMinLevelHeight():Number { return MIN_LEVEL_HEIGHT; }
			/** @see core.IGame.getMaxLevelHeight(). */
			public function getMaxLevelHeight():Number { return MAX_LEVEL_HEIGHT; }
			
			protected function init():void {
				currentState = "splash";
				
				// Set up level loading mechanism.
				file = new FileReference();
				file.addEventListener(Event.SELECT, function():void { file.load(); });
				file.addEventListener(Event.COMPLETE, loadLevel);
			}
			
			protected function play(customLevel:XML = null):void {
				// Initalize game components.
				main.removeAllChildren();
				
				bodies = new LinkedList();
				finishes = new Vector.<Finish>();
				
				new FPS(fps);
				
				virus = new Virus(this);
				bodies.push(virus);
				
				if (customLevel == null) {
					// TODO Load predefined levels!
				} else {
					// Load custom level.
					try {
						main.height = Number(customLevel.@height);
					} catch (e:Error) {
						Alert.show("Invalid map height.", "Symptom > Load custom level");
					}
					main.height = Math.max(MIN_LEVEL_HEIGHT, Math.min(MAX_LEVEL_HEIGHT, main.height));
					main.y = height - main.height;
					
					var xml:XML;
					
					for each (xml in customLevel.Finish) {
						var finish:Finish = new Finish();
						finish.x = xml.x;
						finish.y = xml.y;
						
						finishes.push(finish);
						main.addChild(finish);
					}
					
					for each (xml in customLevel.RBC) {
						var rbc:RBC = new RBC(
							Color.fromString(xml.color), 
							Color.fromString(xml.dna), 
							Math.random()*360
							);
						
						var center:Vector2 = rbc.getCenter();
						rbc.x = xml.x - center.x;
						rbc.y = xml.y - center.y;
						
						bodies.push(rbc);
						main.addChild(rbc);
					}
				}

				virus.x = main.width/2 - virus.width/2;
				virus.y = main.height - virus.height - 5;
				main.addChild(virus);

				// Set up timers.
				loopTimer = new Timer(1000.0 / TICKS_PER_SECOND);
				loopTimer.addEventListener(TimerEvent.TIMER, theLoop);

				var delay:Number = 20;
				launchTimer = new Timer(delay);
				launchTimer.addEventListener(TimerEvent.TIMER, expireLaunch);
				countsToLaunch = LAUNCH_TIME_MS / delay;
				
				// Keyboard events.
				stage.addEventListener(KeyboardEvent.KEY_DOWN, keyListener);
				stage.addEventListener(KeyboardEvent.KEY_UP, keyListener);
				
				currentState = "";
				
				loopTimer.start();
			}
			
			/** Opens the level editor module as a pop-up. */
			protected function openLevelEditor():void {
				moduleInfo = ModuleManager.getModule("LevelEditor.swf");
				moduleInfo.addEventListener(ModuleEvent.READY, _openLevelEditor);
				moduleInfo.load();
			}
			
			// Listener for loadEditor().
			private function _openLevelEditor(e:ModuleEvent):void {
				levelEditor = moduleInfo.factory.create() as LevelEditor;
				levelEditor.init(this);
				
				PopUpManager.addPopUp(levelEditor, this, false);
				PopUpManager.centerPopUp(levelEditor);
			}
			
			protected function loadLevel(e:Event):void {
				var data:ByteArray = file.data;
				
				try {
					var level:XML = XML(data.readUTFBytes(data.bytesAvailable));
				} catch (e:Error) {
					Alert.show("Selected file has invalid content!", "Symptom > Load custom level");
					return;
				}
				
				play(level);
			}
			
			protected function expireLaunch(e:TimerEvent = null):void {
				if (e != null) {
					launchProgress.setProgress(launchTimer.currentCount, countsToLaunch);
					
					if (launchTimer.currentCount < countsToLaunch) {
						return;
					}
				}
				
				launchTimer.reset();
				
				var i:Body = infected.data as Body;
				bodies.pop(infected);
				i.setStyle("removedEffect", fade);
				main.removeChild(i);
				
				infected = null;
			}
			
			protected function keyListener(e:KeyboardEvent):void {
				var isKeyDown:Boolean = (e.type == KeyboardEvent.KEY_DOWN);
				
				if (e.keyCode == 87) { // W
					isUpPressed = isKeyDown;
				} else if (e.keyCode == 83) { // S
					isDownPressed = isKeyDown;
				} else if (e.keyCode == 65) { // A
					isLeftPressed = isKeyDown;
				} else if (e.keyCode == 68) { // D
					isRightPressed = isKeyDown;
				} else if (e.keyCode == Keyboard.SPACE) {
					isSpacePressed = isKeyDown;
				}
			}
			
			protected function theLoop(e:TimerEvent):void {
				var i:int, j:int;
				var b:Body, bi:Body, bj:Body;
				var img:Image;
				var I:LinkedListIterator, J:LinkedListIterator;
				var ni:Node, nj:Node;
				var f:Finish;
				
				/////////////////////////////////////////
				// CHECK FOR VICTORY CONDITION
				/////////////////////////////////////////
				
				for each (f in finishes) {						
					var vc:Vector2 = virus.getCenter();
					var fc:Vector2 = new Vector2(f.x + f.width/2, f.y + f.height/2);
					
					if (vc.subtract(fc).length() < VICTORY_DISTANCE) {
						loopTimer.reset();
						
						Alert.show(
							"You win!", 
							"Symptom", 
							Alert.OK, 
							this, 
							function(e:CloseEvent):void { currentState = "splash"; }
							);
					}
				}
				
				/////////////////////////////////////////
				// CLEAR FORCES
				/////////////////////////////////////////

				for (I = new LinkedListIterator(bodies); I.hasNext();) {
					b = I.next().data as Body;
					
					b.F.reset();
				}
				
				/////////////////////////////////////////
				// ADD FORCES
				/////////////////////////////////////////
				
				var virusDirection:Vector2 = virus.getDirection();
				var virusSpeed:Number = virus.v.length();
				
				// VIRUS MOVEMENT.
				if (infected == null) {
					// WASD movement.
					var m:Vector2 = new Vector2(virusDirection.x * Virus.F_MOVE, virusDirection.y * Virus.F_MOVE);
					if (virusDirection.dot(virus.v) > 0) {
						m.scale((Virus.MAX_SPEED - virusSpeed)/Virus.MAX_SPEED);
					}
								
					if (isUpPressed) {
						virus.F.x += m.x;
						virus.F.y += m.y;
					} 
					if (isDownPressed) {
						virus.F.x -= m.x;
						virus.F.y -= m.y;
					}
					if (isLeftPressed) {
						virus.F.x += m.y;
						virus.F.y -= m.x;
					}
					if (isRightPressed) {
						virus.F.x -= m.y;
						virus.F.y += m.x;
					}
				} else {
					// Launch by applying an impulse.
					if (isSpacePressed && !wasSpacePressed) {
						expireLaunch();
						
						virus.v.acc(virusDirection, Virus.MAX_SPEED);
					}
				}
				
				wasSpacePressed = isSpacePressed;
				
				// DRAG.
				for (I = new LinkedListIterator(bodies); I.hasNext();) {
					b = I.next().data as Body;
					
					b.F.x -= b.v.x * F_DRAG;
					b.F.y -= b.v.y * F_DRAG;
				}
				
				// COLLISION PENALTY FORCES.
				var buffer:Number = 1;
				var k:Number = 10.0;
				
				// Iterate backwards because virus is the last item in 'bodies'.
				i = 0;
				for (I = new LinkedListIterator(bodies); I.hasNext(); i++) {
					bi = I.next().data as Body;;
					
					for (J = new LinkedListIterator(bodies, i+1); J.hasNext();) {
						nj = J.next();
						bj = nj.data as Body;
						
						var p:Vector2 = (bi as IBoundingSphere).getCenter();
						var r:Number  = (bi as IBoundingSphere).getRadius() + buffer;
						var q:Vector2 = (bj as IBoundingSphere).getCenter();
						var s:Number  = (bj as IBoundingSphere).getRadius() + buffer;
						
						var normal:Vector2 = intersect(p, r, q, s);
							
						if (normal != null) {
							if (bi == virus) {
								if (infected != null || launchTimer.running) {
									break;
								}
								
								if (bj is RBC && normal.dot(virusDirection) > Constants.COS_30 && virus.dna == (bj as RBC).color) {
									infected = nj;
									
									virus.F.reset();
									virus.v.reset();
									virus.changeDNA((bj as RBC).dna);

									launchTimer.start();
									
									break;
								}
							}
							
							var d:Number = normal.length();
							normal.normalize();
							var relativeV:Vector2 = bi.v.subtract(bj.v);
							var dot:Number = relativeV.dot(normal);
							
							if (dot < 0) {
								continue;
							}
							
							bi.F.acc(normal, -k * d);
							bj.F.acc(normal,  k * d);
						}
					}
				}
				
				/////////////////////////////////////////
				// UPDATE
				/////////////////////////////////////////
				
				for (I = new LinkedListIterator(bodies); I.hasNext();) {
					b = I.next().data as Body;
					
					b.applyF();
					
					b.step(DT);
					
					// Scroll screen according to virus movement & position.
					if (b == virus && main.y < 0 && b.y + main.y < height/3 && b.v.y < 0) {
						main.y = Math.min(0, main.y - b.v.y * DT);
					}
				}
				
				// If infecting, move virus to infected body center.
				if (infected != null) {
					var pv:Vector2 = (virus as IBoundingSphere).getCenter();
					var pi:Vector2 = (infected.data as IBoundingSphere).getCenter();
					
					virus.x += pi.x - pv.x;
					virus.y += pi.y - pv.y;
				}
				
				if (virusSpeed > Virus.MAX_SPEED) {
					virus.v.normalize(Virus.MAX_SPEED);
				}
				
				/////////////////////////////////////////
				// CONFINE TO BOUNDS
				/////////////////////////////////////////
				
				for (I = new LinkedListIterator(bodies); I.hasNext();) {
					b = I.next().data as Body;
					
					var center:Vector2 = (b as IBoundingSphere).getCenter();
					var radius:Number = (b as IBoundingSphere).getRadius();
					
					if (center.x < radius) {
						b.x += radius - center.x;
						b.v.x *= -RESTITUTION;
					}
					
					if (center.x > main.width - radius) {
						b.x -= center.x - (main.width - radius);
						b.v.x *= -RESTITUTION;
					}
					
					if (center.y < radius) {
						b.y += radius - center.y;
						b.v.y *= -RESTITUTION;
					} 
					
					// Make sure virus does not go off bottom of viewable screen,
					// for other bodies just confine to level bounds.
					var maxHeight:Number = (b == virus) ? height - main.y : main.height;
					if (center.y > maxHeight - radius) {
						b.y -= center.y - (maxHeight - radius);
						b.v.y *= -RESTITUTION;
					}
				}
				
				// DEBUG
				var virusCenter:Vector2 = virus.getCenter();
				virus.graphics.clear();
				virus.graphics.lineStyle(1.0, 0xddbbbb, 1.0);
				virus.graphics.drawCircle(virus.width/2, virus.height/2, virus.getRadius());
			}
			
			/** Returns the children of "main" that are Body objects. */
			protected function getBodies():Array {
				return main.getChildren().filter(
					function(item:*, index:int, a:Array):Boolean { return (item is Body); }
					);
			}
			
			/** Returns the children of "main" that are Finish objects. */
			protected function getFinishes():Array {
				return main.getChildren().filter(
					function(item:*, index:int, a:Array):Boolean { return (item is Finish); }
					);
			}
			
			/**
			 * Tests intersection of two spheres.
			 * 
			 * @param centerA Center point of first sphere.
			 * @param radiusA Radius of first sphere.
			 * @param centerB Center point of second sphere.
			 * @param radiusB Radius of second sphere.
			 * 
			 * @return If spheres intersect, return the contact normal scaled by penetration distance. Otherwise, return null.
			 */
			public static function intersect(centerA:Vector2, radiusA:Number, centerB:Vector2, radiusB:Number):Vector2 {				
				var d:Vector2 = centerB.subtract(centerA);
				
				var l:Number = d.length();
				if (l < radiusA + radiusB) {
					d.normalize(radiusA + radiusB - l);
					return d;
				}
				
				return null;
			}
		]]>
	</mx:Script>
	
  <!--
  /////////////////////////////////////////////////////////
  // COMPONENTS
  /////////////////////////////////////////////////////////
  -->
  <mx:Canvas id="control" 
  	width="{300}">
		<!--- FPS display. -->
		<mx:Label id="fps" width="100%" 
			fontSize="20" textAlign="center" />
		
		<!--- Virus launch display bar. -->
		<mx:ProgressBar id="launchProgress" 
			y="50" 
			width="95%"
			label="Launch Meter" 
			barColor="0x7ecc7b"
			mode="manual" 
			paddingLeft="10" paddingRight="10"
			labelPlacement="center" />
  </mx:Canvas>
  
  <!--- The active playing screen. -->
  <mx:Canvas id="main"
  	x="{control.width}"
  	width="{MAIN_WIDTH}" height="{height}"
  	backgroundColor="0xeedddd"
  	horizontalScrollPolicy="off" verticalScrollPolicy="off" />
  
	
  <!--
  /////////////////////////////////////////////////////////
  // DECLARATIONS
  /////////////////////////////////////////////////////////
  -->
	<!--- Effect for fading out destroyed bodies. -->
	<mx:Fade id="fade" alphaFrom="1.0" alphaTo="0.0" easingFunction="{Cubic.easeIn}" />
	
  <!--
  /////////////////////////////////////////////////////////
  // STATES
  /////////////////////////////////////////////////////////
  -->
  <mx:states>
  	<mx:State name="splash">
  		<mx:RemoveChild target="{main}" />
  		<mx:RemoveChild target="{control}" />
  		
  		<mx:AddChild>
  			<mx:Panel 
  				x="250" y="50"
  				width="300" height="480"
  				horizontalAlign="center" 
  				horizontalScrollPolicy="off" verticalScrollPolicy="off"
  				layout="vertical"
  				cornerRadius="0">
  				
  				<mx:Image source="assets/logo.png" />
  				
  				<mx:Text width="100%" 
  					fontSize="9" textAlign="center" fontAntiAliasType="normal" fontSharpness="400"
  					paddingTop="10" paddingBottom="15" 
  					selectable="false">
  					<mx:htmlText>
	  					<![CDATA[<i>Symptom</i> is a 2D scroller with a twist. You're the virus!<br /><br />Use your <b>MOUSE</b> to guide the virus and the <b>WASD</b> keys to move. Infect a cell by moving forward towards it - once inside, "launch" away by pressing the <b>SPACE</b> bar!]]>
  					</mx:htmlText>
  				</mx:Text>
  				
  				<mx:Image source="assets/directions.png" />
  				
  				<mx:Spacer height="10" />
  				
	  			<mx:Button label="Play!" 
	  				width="100%" height="50" 
	  				enabled="false"
	  				click="play();" />
	  			<mx:Button label="Load custom level"
	  				width="100%" height="30"
	  				click="file.browse([new FileFilter('Symptom Level', '*.xml')]);" alpha="1.0"/>
	  			<mx:Button label="Create your own level!" 
	  				width="100%" height="30"
	  				click="openLevelEditor();" />
  			</mx:Panel>
  		</mx:AddChild>
  	</mx:State>
  </mx:states>
	
</mx:Application>