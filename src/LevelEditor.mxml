<?xml version="1.0" encoding="utf-8"?>
<!--- The level editor. -->
<mx:Module 
	xmlns:mx="http://www.adobe.com/2006/mxml" 
	layout="absolute" 
	width="600" height="500"
  backgroundColor="white"
  backgroundAlpha="1.0"
	verticalScrollPolicy="off"
	creationComplete="init();">
  
  <mx:Style>
  	.h1 {
			fontSize: 12;
			fontWeight: bold;
  	}
  	
  	.h2 {
  		fontWeight: bold;
  	}
  </mx:Style>
  
  <mx:Script>
  	<![CDATA[
  		import core.IBoundingSphere;
  		import core.Body;
  		import core.Color;
  		import units.RBC;
  		import utils.Vector2;
  		import utils.LinkedList.*;
  		import mx.managers.PopUpManager;
  		import mx.controls.Alert;
  		import mx.events.CloseEvent;
  		
  		/** Sample rbc on control panel. */
  		protected var rbc:RBC;
  		
  		/** The selected new Body to insert. */
  		protected var selection:Body;
  		
  		/** Is the current mouse-selected position for the new Body valid? */
  		protected var isValidPosition:Boolean = false;
  		/** Is the user currently picking up a Body? */
  		protected var isPickUp:Boolean = false;
  		
  		/** XML output of this level. */
  		[Bindable]
  		protected var output:XML;
  		
  		public function init():void {
  			callLater(_init);
  		}
  		
  		// Wait for components to be created.
  		private function _init():void {
  			rbc = new RBC(new Vector2(0, 0), Color.RED, Color.BLUE);
  			rbc.useHandCursor = rbc.mouseChildren = rbc.buttonMode = true;
  			rbc.addEventListener(MouseEvent.CLICK, selectRBC);
  			sampleRBC.addChild(rbc);
  		}
  		
  		protected function selectRBC(e:MouseEvent):void {
  			selection = new RBC(
  				new Vector2(0, 0), 
  				rbc.color, 
  				rbc.dna
  				);
  			selection.useHandCursor = selection.mouseChildren = selection.buttonMode = true;
   			selection.alpha = 0.6;
  			onMouseMove();
  			addChild(selection);
  			
  			addSelectionListeners();
  		}
  		
  		protected function addSelectionListeners():void {
  			addEventListener(MouseEvent.MOUSE_MOVE, onMouseMove);
  			addEventListener(MouseEvent.MOUSE_DOWN, onMouseDown);
  		}
  		
  		protected function removeSelectionListeners():void {
  			removeEventListener(MouseEvent.MOUSE_MOVE, onMouseMove);
  			removeEventListener(MouseEvent.MOUSE_DOWN, onMouseDown);
  		}
  		
  		/** Centers the selected body to the mouse and checks if this position is valid. */
  		protected function onMouseMove(e:MouseEvent = null):void {
  			selection.x = mouseX - selection.width/2;
  			selection.y = mouseY - selection.height/2;
  			
  			// Check if within screen bounds.
  			if (selection.x > 0 && selection.x < main.width - selection.width && 
  					selection.y > 0 && selection.y < main.height - selection.height) 
  					{
  				// Check for intersections with other bodies.
  				var bodies:Array = main.getChildren();
  				
  				var p:Vector2 = (selection as IBoundingSphere).getCenter();
  				var r:Number = (selection as IBoundingSphere).getRadius();
  				
  				var intersect:Boolean = false;
  				for each (var b:IBoundingSphere in bodies) {
	  				var q:Vector2 = b.getCenter();
	  				q.y -= scroll.scrollPosition;
	  				var s:Number = b.getRadius();
  					
  					if (Symptom.intersect(p, r, q, s) != null) {
  						intersect = true;
  						break;
  					}
  				}
  				
  				if (!intersect) {
	  				selection.filters = [glowGreen];
	  				isValidPosition = true;
	  				return;
  				}
  			}
  			
  			selection.filters = [glowRed];
  			isValidPosition = false;
  		}
  		
  		/** Places selected body in the current mouse position if possible, otherwise nullifies selection. */
  		protected function onMouseDown(e:MouseEvent):void {
  			// Ignore the first click during a pick up to avoid inserting the body back into the same spot.
  			if (isPickUp) {
  				isPickUp = false;
  				return;
  			}
  			
  			removeChild(selection);
  			
  			if (isValidPosition) {
  				selection.alpha = 1;
  				selection.filters = null;
  				
  				main.addChild(selection);
  				selection.x = mouseX - selection.width/2;
  				selection.y = mouseY + scroll.scrollPosition - selection.height/2;
  				
  				selection.addEventListener(MouseEvent.MOUSE_DOWN, pickUp);
  				
  				heightSlider.minimum = Math.max(selection.y + selection.height, heightSlider.minimum);
  			}

  			selection = null;
  			removeSelectionListeners();
  		}
  		
  		/** "Picks up" an already placed Body in the level. */
  		protected function pickUp(e:MouseEvent):void {
  			isPickUp = true;
  				
  			selection = e.currentTarget as Body;
  			
  			selection.removeEventListener(MouseEvent.MOUSE_DOWN, pickUp);
  			main.removeChild(selection);
  			
  			selection.alpha = 0.6;
  			onMouseMove();
  			addChild(selection);
  			
  			addSelectionListeners();
  		}
  		
  		protected function save():void {
  			output =
	  			<Symptom height={Math.round(main.height)}>
	  			</Symptom>;
  			
  			var bodies:Array = main.getChildren();
  			
  			for each (var b:Body in bodies) {
  				var xml:XML;
  				
  				if (b is RBC) {
  					var rbc:RBC = b as RBC;
  					
  					xml = 
  						<RBC>
  							<x>{b.x}</x>
  							<y>{b.y}</y>
  							<color>{Color.toString(rbc.color)}</color>
  							<dna>{Color.toString(rbc.dna)}</dna>
  						</RBC>;
  				} else {
  					throw new Error("Unrecognized Body.");
  				}
  				
  				output.appendChild(xml);
  			}
  			
  			new FileReference().save(output, "MySymptomLevel.xml");
  		}
  		
  		protected function reset():void {
  			Alert.show("Are you sure you want to reset this level?", 
  				"Symptom Level Editor", 
  				Alert.YES | Alert.NO, 
  				this, 
  				_reset, 
  				null, 
  				Alert.NO);		
  		}
  		
  		// Listener for reset().
  		private function _reset(e:CloseEvent):void {
  			if (e.detail == Alert.YES) {	
  				main.height = heightSlider.value = 600;
  				scroll.scrollPosition = 0;
  				main.removeAllChildren();
  			}
  		}
  		
  		protected function close():void {
  			Alert.show("Are you sure you want to exit the level editor?", 
  				"Symptom Level Editor", 
  				Alert.YES | Alert.NO, 
  				this, 
  				_close, 
  				null, 
  				Alert.NO);
  		}
  		
  		// Listener for close().
  		private function _close(e:CloseEvent):void {
  			if (e.detail == Alert.YES) {
  				PopUpManager.removePopUp(this); 				
  			}
  		}
  	]]>
  </mx:Script>
  
  <!--
  /////////////////////////////////////////////////////////
  // COMPONENTS
  /////////////////////////////////////////////////////////
  -->
  <mx:Canvas id="main" 
  	width="400" height="600" 
  	backgroundColor="0xffcccc"
  	verticalScrollPolicy="off" />
  	
  <mx:VScrollBar id="scroll" 
  	x="400"
  	width="25" height="100%"
  	pageSize="{height}" lineScrollSize="{height}"
  	minScrollPosition="0" maxScrollPosition="{main.height - height}"
  	scroll="main.y = -scroll.scrollPosition;" />
  
  <mx:VBox 
  	x="430" y="10"
  	width="160"
  	horizontalAlign="center">
  	
  	<mx:Label text="Map height" styleName="h1" />
		<mx:HSlider id="heightSlider"
			width="100%"
			minimum="600" maximum="1200" 
			labels="['Min.', 'Max.']"
			liveDragging="false"
			change="main.height = heightSlider.value;" />
			
		<mx:HRule width="100%" height="15" />
		
		<mx:Canvas id="sampleRBC" />
		<mx:HBox paddingTop="5">
			<mx:Label text="Cell" paddingTop="4" styleName="h2" />
			<mx:Image source="assets/editor/red_box.swf"    click="rbc.setColors(Color.RED,    rbc.dna);" />
			<mx:Image source="assets/editor/blue_box.swf"   click="rbc.setColors(Color.BLUE,   rbc.dna);" />
			<mx:Image source="assets/editor/green_box.swf"  click="rbc.setColors(Color.GREEN,  rbc.dna);" />
			<mx:Image source="assets/editor/yellow_box.swf" click="rbc.setColors(Color.YELLOW, rbc.dna);" />
		</mx:HBox>
		<mx:HBox>
			<mx:Label text="DNA" paddingTop="4" styleName="h2" />
			<mx:Image source="assets/editor/red_box.swf"    click="rbc.setColors(rbc.color, Color.RED);" />
			<mx:Image source="assets/editor/blue_box.swf"   click="rbc.setColors(rbc.color, Color.BLUE);"/>
			<mx:Image source="assets/editor/green_box.swf"  click="rbc.setColors(rbc.color, Color.GREEN);"/>
			<mx:Image source="assets/editor/yellow_box.swf" click="rbc.setColors(rbc.color, Color.YELLOW);"/>
		</mx:HBox>
		
		<mx:HRule width="100%" height="15" />
		
		<mx:Button width="100%" height="40" label="Save!" click="save();" />
		<mx:Button width="100%" label="Reset" click="reset();" />
		<mx:Button width="100%" label="Cancel" click="close();" />
  </mx:VBox>
	
  <!--
  /////////////////////////////////////////////////////////
  // FILTERS
  /////////////////////////////////////////////////////////
  -->
  <mx:GlowFilter id="glowRed"   color="0xff0000" blurX="30" blurY="30" inner="true" />
  <mx:GlowFilter id="glowGreen" color="0x00ff00" blurX="30" blurY="30" inner="true" />
	
</mx:Module>
